*$FROM LibraryEx
$EXTERN ArgList, Map, LoadExpr;

/*Индивидуальный вариант
Символьная функция
Регулярное выражение описано следующим абстрактным синтаксисом:
RegEx → ∅ | ε | SYMBOL | RegEx ⋃ RegEx | RegEx · RegEx | RegEx*
Здесь ∅ — пустое множество, ⋃ — объединение, · — конкатенация.
Требуется написать функцию <Simplify t.RegEx> == t.RegEx, упрощающую регулярное выражение:
    r∅ = ∅r = ∅
    r ∪ ∅ = ∅ ∪ r = r
    rε = εr = r
    ε* = ∅* = ε
*/

$ENTRY Go{
	= <Main <ArgList>>
}

Main{
	(e.ProgName) /*empty*/  = <RegexProcess ('regextest')>;
	(e.ProgName) e.FileList = <Map RegexProcess e.FileList>;
}

$ENTRY RegexProcess{
	(e.FileName) = <Prout <Simplify <LoadExpr e.FileName>>>
}
/*
t.RegEx ::= EmptySet
	| EmptyWord
	| s.SYMBOL 
	| (t.RegEx Union t.RegEx)
	| (t.RegEx Concat t.RegEx)
	| (t.RegEx Star)
	
<Simplify t.RegEx> == t.RegEx
*/

Simplify{
	(t.A Union t.B),
		(<Simplify t.A>) (<Simplify t.B>) : {
			(t.RegEx) (EmptySet) = t.RegEx;
			(EmptySet) (t.RegEx) = t.RegEx;
			(t.RegA) (t.RegB) = (t.RegA Union t.RegB);
		};
	(t.A Concat t.B),
		(<Simplify t.A>) (<Simplify t.B>) : {
			(t.RegEx) (EmptySet) = EmptySet;
			(EmptySet) (t.RegEx) = EmptySet;
			(t.RegEx) (EmptyWord) = t.RegEx;
			(EmptyWord)(t.RegEx) = t.RegEx;
			(t.RegA) (t.RegB) = (t.RegA Concat t.RegB);
		};
	(t.RegEx Star), (<Simplify t.RegEx>) : {
		(EmptySet) = EmptyWord;
		(EmptyWord) = EmptyWord;
		(t.Reg) = (t.Reg Star)
	};
	t.Other = t.Other;
}
